#summary Corpora anaysis and search examples.
<wiki:toc max_depth="3" />
= Corpora anaysis and search =
The SPARQL query language can be directly used for calculating system performance metrics as well as *for various drill-down searches in the gold standard corpora and experiment results*. It is very flexible and implement easy querying mechanism.

== Example Endpoints ==
With Impacts
 * Open Mutation Miner on Enzyminer Corpus:
   * [http://138.119.12.53:8080/openrdf-workbench/repositories/enzyminer-omm/query]
 * Open Mutation Miner on OMM Impact Corpus:
   * [http://138.119.12.53:8080/openrdf-workbench/repositories/impact-omm/query]
 * Mutation Impact Extraction System on Enzyminer Corpus:
   * [http://138.119.12.53:8080/openrdf-workbench/repositories/enzyminer-mies/query]
 * Mutation Impact Extraction System on OMM Impact Corpus:
   * [http://138.119.12.53:8080/openrdf-workbench/repositories/impact-mies/query]
Without Impacts
 * Open Mutation Miner on DHLA Corpus:
   * [http://138.119.12.53:8080/openrdf-workbench/repositories/dhla-omm/query]
 * Mutation Impact Extraction System on DHLA Corpus:
   * [http://138.119.12.53:8080/openrdf-workbench/repositories/dhla-mies/query]

== Example SPARQL Queries ==

=== Get document ids in gold standard corpus (number of documents in corpus) ===
{{{
SELECT DISTINCT ?pubmed_id
WHERE {
GRAPH <http://example.com/gold-standard.rdf> {
      ?document a sio:SIO_000154 .
      ?document sio:SIO_000629 ?pubmed_record .
      ?pubmed_record a lsrn:PMID_Record .
      ?pubmed_record sio:SIO_000008 ?pubmed_identifier .
      ?pubmed_identifier a lsrn:PMID_Identifier .
      ?pubmed_identifier sio:SIO_000300 ?pubmed_id .
}
}
}}}


=== Get mutations (including combined mutations - mutation series) ===
{{{
SELECT DISTINCT ?pubmed_id ?mut_id
WHERE {
GRAPH <http://example.com/gold-standard.rdf> {
      ?document a sio:SIO_000154 .
      ?document sio:SIO_000629 ?pubmed_record .
      ?pubmed_record a lsrn:PMID_Record .
      ?pubmed_record sio:SIO_000008 ?pubmed_identifier .
      ?pubmed_identifier a lsrn:PMID_Identifier .
      ?pubmed_identifier sio:SIO_000300 ?pubmed_id .

 ?ann_mutation a ao:Annotation .
      ?ann_mutation aof:annotatesDocument ?document .
      ?ann_mutation ao:hasTopic ?mutation .
      ?mutation a mieo:CombinedAminoAcidSequenceChange .
      ?mutation sio:SIO_000673 ?mut_identifier .
      ?mut_identifier a mieo:CombinedAminoAcidSequenceChange_Identifier .
      ?mut_identifier sio:SIO_000300 ?mut_id .
	# for OMM corpus which contains 'unknown' (='n/a') mutations
      FILTER (!regex(str(?mut_id),"UNKNOWN"))
}
}
}}}

=== Get impact sentences ===
{{{
SELECT DISTINCT ?pubmed_id ?property_change ?text_selector
WHERE {
  GRAPH <http://example.com/gold-standard.rdf>
   {
      ?document a sio:SIO_000154 .
      ?document sio:SIO_000629 ?pubmed_record .
      ?pubmed_record sio:SIO_000008 ?pubmed_identifier .
      ?pubmed_identifier sio:SIO_000300 ?pubmed_id .

      ?ann a ao:Annotation .
      ?ann aof:annotatesDocument ?document .
      ?ann ao:hasTopic ?property_change .
      ?property_change a ?property_change_class .
      ?property_change_class rdfs:subClassOf mieo:ProteinPropertyChange .

      FILTER NOT EXISTS {
          ?ann ao:hasTopic ?topic3 .
          FILTER (?property_change != ?topic3) .
      }

      ?ann ao:context ?text_selector .
      ?text_selector a aos:TextSelector . 
     # ?text_selector aos:exact ?text .

   } 
}
}}}

=== Get false negatives ===
The SPARQL negation-related features proved especially useful in this effort because they allowed us to identify false negatives â€“ cases presented in gold standard and absent from system results, thus identifying potential targets for optimisation. The following query represents such a use case where the FILTER NOT
EXISTS feature is applied to exclude correct system results for mutation grounding from the set of all results.
<br>
Endpoints: <br>
 * Open Mutation Miner on DHLA Corpus:
   * [http://138.119.12.53:8080/openrdf-workbench/repositories/dhla-omm/query]
 * Mutation Impact Extraction System on DHLA Corpus:
   * [http://138.119.12.53:8080/openrdf-workbench/repositories/dhla-mies/query]
Query:
{{{
PREFIX aof:<http://purl.org/ao/foaf/>
PREFIX foaf:<http://xmlns.com/foaf/0.1/>
PREFIX aot:<http://purl.org/ao/types/>
PREFIX aos:<http://purl.org/ao/selectors/>
PREFIX mieo:<http://cbakerlab.unbsj.ca:8080/ontologies/mutation-impact-extraction-ontology.owl#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX ao:<http://purl.org/ao/>
PREFIX pav:<http://purl.org/pav/>
PREFIX lsrn:<http://purl.oclc.org/SADI/LSRN/>
PREFIX sio:<http://semanticscience.org/resource/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?pubmed_id ?wt_residue ?position_value ?mut_residue ?uniprot_record_id

WHERE {

GRAPH <http://example.com/gold-standard.rdf> {

      ?document a sio:SIO_000154 .
      ?document sio:SIO_000629 ?pubmed_record .
      ?pubmed_record a lsrn:PMID_Record .
      ?pubmed_record sio:SIO_000008 ?pubmed_identifier .
      ?pubmed_identifier a lsrn:PMID_Identifier .
      ?pubmed_identifier sio:SIO_000300 ?pubmed_id .

      ?ann_mutation a ao:Annotation .
      ?ann_mutation aof:annotatesDocument ?document .
      ?ann_mutation ao:hasTopic ?mutation .
      ?mutation a mieo:CombinedAminoAcidSequenceChange .
      ?mutation sio:SIO_000059 ?singular_mutation .
      ?singular_mutation a mieo:AminoAcidSubstitution .

      ?singular_mutation mieo:mutationHasWildtypeResidue ?wt_residue .
      ?singular_mutation mieo:mutationHasMutantResidue ?mut_residue .
      ?singular_mutation mieo:mutationHasPosition ?position .
      ?position a sio:SIO_000056 .
      ?position sio:SIO_000300 ?position_value .

      ?ann_mutation_application a ao:Annotation .
      ?ann_mutation_application aof:annotatesDocument ?document .
      ?ann_mutation_application ao:hasTopic ?mutation_application .
      ?mutation_application a mieo:ProteinMutationApplication .
      ?mutation_application mieo:isApplicationOfMutation ?mutation .
      ?mutation_application mieo:isApplicationOfMutationToProtein ?protein .

      ?ann_protein a ao:Annotation .
      ?ann_protein aof:annotatesDocument ?document .
      ?ann_protein ao:hasTopic ?protein .
      ?protein a mieo:ProteinVariant .
      ?protein sio:SIO_000629 ?uniprot_record .
      ?uniprot_record a lsrn:UniProt_Record .
      ?uniprot_record sio:SIO_000008 ?uniprot_record_identifier .
      ?uniprot_record_identifier a lsrn:UniProt_Identifier .
      ?uniprot_record_identifier sio:SIO_000300 ?uniprot_record_id .
      FILTER (?uniprot_record_id != "")
}

FILTER NOT EXISTS {
GRAPH <http://example.com/experiment.rdf> {
      
      ?document2 a sio:SIO_000154 .
      ?document2 sio:SIO_000629 ?pubmed_record2 .
      ?pubmed_record2 a lsrn:PMID_Record .
      ?pubmed_record2 sio:SIO_000008 ?pubmed_identifier2 .
      ?pubmed_identifier2 a lsrn:PMID_Identifier .
      ?pubmed_identifier2 sio:SIO_000300 ?pubmed_id .

      ?ann_mutation2 a ao:Annotation .
      ?ann_mutation2 aof:annotatesDocument ?document2 .
      ?ann_mutation2 ao:hasTopic ?mutation2 .
      ?mutation2 a mieo:CombinedAminoAcidSequenceChange .
      ?mutation2 sio:SIO_000059 ?singular_mutation2 .
      ?singular_mutation2 a mieo:AminoAcidSubstitution .

      ?singular_mutation2 mieo:mutationHasWildtypeResidue ?wt_residue .
      ?singular_mutation2 mieo:mutationHasMutantResidue ?mut_residue .
      ?singular_mutation2 mieo:mutationHasPosition ?position2 .
      ?position2 a sio:SIO_000056 .
      ?position2 sio:SIO_000300 ?position_value .

      ?ann_mutation_application2 a ao:Annotation .
      ?ann_mutation_application2 aof:annotatesDocument ?document2 .
      ?ann_mutation_application2 ao:hasTopic ?mutation_application2 .
      ?mutation_application2 a mieo:ProteinMutationApplication .
      ?mutation_application2 mieo:isApplicationOfMutation ?mutation2 .
      ?mutation_application2 mieo:isApplicationOfMutationToProtein ?protein2 .

      ?ann_protein2 a ao:Annotation .
      ?ann_protein2 aof:annotatesDocument ?document2 .
      ?ann_protein2 ao:hasTopic ?protein2 .
      ?protein2 a mieo:ProteinVariant .
      ?protein2 sio:SIO_000629 ?uniprot_record2 .
      ?uniprot_record2 a lsrn:UniProt_Record .
      ?uniprot_record2 sio:SIO_000008 ?uniprot_record_identifier2 .
      ?uniprot_record_identifier2 a lsrn:UniProt_Identifier .
      ?uniprot_record_identifier2 sio:SIO_000300 ?uniprot_record_id .
      FILTER (?uniprot_record_id != "") 
}
}
} ORDER BY ?pubmed_id 


}}}